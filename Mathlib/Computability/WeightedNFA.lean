/-
Copyright (c) 2025 Rudy Peterson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rudy Peterson
-/
module

public import Mathlib.Computability.NFA
public import Mathlib.Computability.WeightedDFA
public import Mathlib.Algebra.Module.BigOperators
public import Mathlib.Algebra.BigOperators.Ring.Finset

/-!
# Weighted Nondeterministic Finite Automata

A (`Îµ`-free) Weighted Nondeterministic Finite Automata (WNFA) is a state machine that describes a
weighted language by assinging an input string a weight. The weight of the string is determined by
the sum of path weights taken through the state machine.

Every transition in a WNFA produces a weight, which is an element of a semiring.
The weight of a path, a sequence of transitions, is the in-order multiplication of all of its
constituent transitions.

Note that this construction relies upon [Fintype Ïƒ] for its core definitions and lemmas.
-/

@[expose] public section

universe u v k

/-- A Weighted NFA (`ğ“`) over a semiring (`ğ“¦ = (Îº, âŠ•, âŠ—, 0, 1)`)
is a 5-tuple (`(Î±, Ïƒ, step, start, final)`) where
* (`Î±`) is a (finite) alphabet.
* (`Ïƒ`) is a (finite) set of states.
* (`step : Ïƒ â†’ Î± â†’ Ïƒ â†’ Îº`) is a (finite) set of transitions.
* (`start : Ïƒ â†’ Îº`) is a weighting function assigning states their start values.
* (`final : Ïƒ â†’ Îº`) is a weighting function assigning states their final values.
-/
structure WNFA (Î± : Type u) (Ïƒ : Type v) (Îº : Type k) where
  /-- The NFA's transition function -/
  step : Ïƒ â†’ Î± â†’ Ïƒ â†’ Îº
  /-- Initial weights. -/
  start : Ïƒ â†’ Îº
  /-- Final weights. -/
  final : Ïƒ â†’ Îº

namespace WNFA

variable {Î± : Type u} {Îº : Type k}

section basic

variable {Ïƒ : Type v} [W : Semiring Îº]

@[simp]
theorem finset_sum_apply (S : Finset Ïƒ) (f : Ïƒ â†’ WeightedLanguage Î± Îº) (x : List Î±) :
    (âˆ‘ s âˆˆ S, f s) x = âˆ‘ s âˆˆ S, f s x := by
  apply Finset.sum_apply

instance : Inhabited (WNFA Î± Ïƒ Îº) :=
  âŸ¨WNFA.mk (fun _ _ â†¦ 0) 0 0âŸ©

variable (M : WNFA Î± Ïƒ Îº) [Fintype Ïƒ]

/-- `M.stepSet S a` sums all transitions in `M` from `S` along character `a`.
For every `s : Ïƒ`, we multiply the weight `S s` with all resulting weights from `M.step s a`, then
sums all results together. -/
def stepSet (S : Ïƒ â†’ Îº) (a : Î±) : Ïƒ â†’ Îº :=
  âˆ‘ s : Ïƒ, S s â€¢ M.step s a

@[simp]
theorem stepSet_add (S1 S2 : Ïƒ â†’ Îº) (a : Î±) :
    M.stepSet (S1 + S2) a = M.stepSet S1 a + M.stepSet S2 a := by
  ext s
  simp [stepSet, W.right_distrib, Finset.sum_add_distrib]

@[simp]
theorem stepSet_const_zero {a : Î±} : M.stepSet 0 a = 0 := by
  ext s
  simp [stepSet]

theorem stepSet_smul (w : Îº) (S : Ïƒ â†’ Îº) (a : Î±) :
    M.stepSet (w â€¢ S) a = w â€¢ M.stepSet S a := by
  ext s
  simp [stepSet, Finset.mul_sum, W.mul_assoc]

/-- `M.evalFrom S x` is the weightings obtained by traversing `M` with string `x` starting
from `S`. -/
def evalFrom (S : Ïƒ â†’ Îº) : List Î± â†’ Ïƒ â†’ Îº :=
  List.foldl M.stepSet S

@[simp]
theorem evalFrom_nil (S : Ïƒ â†’ Îº) : M.evalFrom S [] = S :=
  rfl

@[simp]
theorem evalFrom_cons (S : Ïƒ â†’ Îº) (a : Î±) (x : List Î±) :
    M.evalFrom S (a :: x) = M.evalFrom (M.stepSet S a) x :=
  rfl

@[simp]
theorem evalFrom_append (S : Ïƒ â†’ Îº) (x y : List Î±) :
    M.evalFrom S (x ++ y) = M.evalFrom (M.evalFrom S x) y := by
  simp only [evalFrom, List.foldl_append]

@[simp]
theorem evalFrom_add (S1 S2 : Ïƒ â†’ Îº) (x : List Î±) :
    M.evalFrom (S1 + S2) x = M.evalFrom S1 x + M.evalFrom S2 x := by
  induction x generalizing S1 S2 with
  | nil => simp
  | cons a x ih => simp [ih]

/-- `M.acceptsFrom S` is the weighted language produced by `M` starting from states in `S`. -/
def acceptsFrom (S : Ïƒ â†’ Îº) : WeightedLanguage Î± Îº :=
  fun x â†¦ âˆ‘ s : Ïƒ, M.evalFrom S x s * M.final s

@[simp]
theorem acceptsFrom_nil (S : Ïƒ â†’ Îº) : M.acceptsFrom S [] = âˆ‘ s : Ïƒ, S s * M.final s :=
  rfl

@[simp]
theorem acceptsFrom_cons (S : Ïƒ â†’ Îº) (a : Î±) (x : List Î±) :
    M.acceptsFrom S (a :: x) = M.acceptsFrom (M.stepSet S a) x :=
  rfl

@[simp]
theorem acceptsFrom_add (S1 S2 : Ïƒ â†’ Îº) :
    M.acceptsFrom (S1 + S2) = M.acceptsFrom S1 + M.acceptsFrom S2 := by
  ext x
  simp [acceptsFrom, W.right_distrib, Finset.sum_add_distrib]

@[simp]
theorem acceptsFrom_const_zero :
    M.acceptsFrom 0 = 0 := by
  ext x
  simp only [WeightedLanguage.zero_def]
  induction x with
  | nil => simp
  | cons a x ih => simp [M.stepSet_const_zero, ih]

theorem acceptsFrom_sum {Î¹ : Type*} (I : Finset Î¹) (f : Î¹ â†’ Ïƒ â†’ Îº) :
    M.acceptsFrom (âˆ‘ i âˆˆ I, f i) = âˆ‘ i âˆˆ I, M.acceptsFrom (f i) := by
  open scoped Classical in
  ext x
  induction I using Finset.induction with
  | empty => simp [show (0 : List Î± â†’ Îº) x = (0 : Îº) by rfl]
  | insert i I hi ih => simp [Finset.sum_insert hi, ih]

theorem acceptsFrom_sum_Fintype {Î¹ : Type*} [Fintype Î¹] (f : Î¹ â†’ Ïƒ â†’ Îº) :
    M.acceptsFrom (âˆ‘ i : Î¹, f i) = âˆ‘ i : Î¹, M.acceptsFrom (f i) := by
  rw [acceptsFrom_sum]

theorem acceptsFrom_smul (w : Îº) (S : Ïƒ â†’ Îº) :
    M.acceptsFrom (w â€¢ S) = w â€¢ M.acceptsFrom S := by
  ext x
  induction x generalizing w S with
  | nil => simp [Finset.mul_sum, W.mul_assoc]
  | cons a x ih => simp [stepSet_smul, ih]

@[simp]
theorem acceptsFrom_compose_cons (S : Ïƒ â†’ Îº) (a : Î±) :
    M.acceptsFrom S âˆ˜ (a :: Â·) = M.acceptsFrom (M.stepSet S a) :=
  rfl

/-- `M.accepts` is the weighted language of `M`. -/
def accepts : WeightedLanguage Î± Îº := M.acceptsFrom M.start

end basic

section toNFA

/- ### Weighted to unweighted NFA

We cannot use `Bool` for the weight type, since the Mathlib instance for `Add Bool` uses `xor`, not
`or`. Instead we use a type isomorphic to `Bool`.

-/

variable {Ïƒ : Type} (M : WNFA Î± Ïƒ (WithZero Unit))

@[simp]
lemma wzu_add_eq_one (x y : WithZero Unit) :
    x + y = 1 â†” (x = 1 âˆ¨ y = 1) := by
  rcases (WDFA.wzu_zero_or_one x) with rfl | rfl <;>
  rcases (WDFA.wzu_zero_or_one y) with rfl | rfl <;> tauto

@[simp]
lemma wzu_mul_eq_one (x y : WithZero Unit) :
    x * y = 1 â†” (x = 1 âˆ§ y = 1) := by
  rcases (WDFA.wzu_zero_or_one x) with rfl | rfl <;>
  rcases (WDFA.wzu_zero_or_one y) with rfl | rfl <;> tauto

/-- `getSt S` is the set of states that map to `1` in `S`. -/
private def getSet (S : Ïƒ â†’ WithZero Unit) : Set Ïƒ :=
  { s | S s = 1 }

@[simp]
private theorem getSet_zero : getSet (0 : Ïƒ â†’ WithZero Unit) = âˆ… := by
  simp [getSet]

@[simp]
private theorem getSet_add (S1 S2 : Ïƒ â†’ WithZero Unit) :
    getSet (S1 + S2) = getSet S1 âˆª getSet S2 := by
  ext q
  simp [getSet]

/-- `M.toNFAStart` is the start states of `M.toNFA`. -/
@[simp]
def toNFAStart : Set Ïƒ := getSet M.start

/-- `M.toNFAAccept` is the accept states of `M.toNFA`. -/
@[simp]
def toNFAAccept : Set Ïƒ := getSet M.final

/-- `M.toNFAStep` is the step function of `M.toNFA`. -/
@[simp]
def toNFAStep (s : Ïƒ) (a : Î±) : Set Ïƒ := getSet <| M.step s a

/-- `M.toNFA` is an unweighted NFA constructed from a "boolean"-weighted WNFA `M`. -/
@[simps]
def toNFA : NFA Î± Ïƒ where
  step := M.toNFAStep
  start := M.toNFAStart
  accept := M.toNFAAccept

theorem exists_sum_Finset_eq_one {S : Finset Ïƒ} {f : Ïƒ â†’ WithZero Unit} :
    (âˆƒ s âˆˆ S, f s = 1) â†” âˆ‘ s âˆˆ S, f s = 1 := by
  open scoped Classical in
  induction S using Finset.induction with
  | empty => simp
  | insert q S hq ih => simp [Finset.sum_insert hq, ih]

variable [Fintype Ïƒ]

theorem exists_sum_Fintype_eq_one {f : Ïƒ â†’ WithZero Unit} :
    (âˆƒ s, f s = 1) â†” âˆ‘ s : Ïƒ, f s = 1 := by
  simp [â†exists_sum_Finset_eq_one]

lemma toNFA_stepSet {S : Ïƒ â†’ WithZero Unit} {a : Î±} :
    M.toNFA.stepSet (getSet S) a = getSet (M.stepSet S a) := by
  ext s
  simp [NFA.stepSet, stepSet, getSet, â†exists_sum_Fintype_eq_one]

lemma toNFA_acceptsFrom {x : List Î±} {S : Ïƒ â†’ WithZero Unit} :
    x âˆˆ M.toNFA.acceptsFrom (getSet S) â†” M.acceptsFrom S x = 1 := by
  induction x generalizing S
  case nil => simp [getSet, â†exists_sum_Fintype_eq_one]
  case cons a x ih =>
    simp only [NFA.cons_mem_acceptsFrom, toNFA_stepSet, ih]
    rfl

theorem toNFA_accepts {x : List Î±} : x âˆˆ M.toNFA.accepts â†” M.accepts x = 1 := by
  apply toNFA_acceptsFrom

end toNFA

section empty

variable (w : Îº) [W : Semiring Îº]

/-- `WNFA.empty w` is a WNFA accepting the nil-only weighted language with weight `w`. -/
def empty : WNFA Î± Unit Îº where
  step := fun _ _ _ â†¦ 0
  start := Function.const Unit w
  final := Function.const Unit 1

@[simp]
theorem empty_step : (empty (Î±:=Î±) w).step = fun _ (_ : Î±) _ â†¦ 0 :=
  rfl

@[simp]
theorem empty_start : (empty (Î±:=Î±) w).start = Function.const Unit w :=
  rfl

@[simp]
theorem empty_final : (empty (Î±:=Î±) w).final = Function.const Unit 1 :=
  rfl

@[simp]
theorem stepSet_empty {S : Unit â†’ Îº} {a : Î±} : (empty (Î±:=Î±) w).stepSet S a = 0 := by
  ext âŸ¨âŸ©
  simp [stepSet]

theorem accepts_empty : (empty (Î±:=Î±) w).accepts = w â€¢ 1 := by
  ext x
  rw [accepts]
  cases x with
  | nil => simp
  | cons a x => simp

end empty

section char

variable (a : Î±) [DecidableEq Î±] [W : Semiring Îº]

/-- `M.charStart` is the start states of `M.char`. -/
@[simp]
def charStart (s : Bool) : Îº := â†‘s.not.toNat

/-- `M.charFinal` is the final states of `M.char`. -/
@[simp]
def charFinal (s : Bool) : Îº := â†‘s.toNat

/-- `M.charStep` is the step function of `M.char`. -/
@[simp]
def charStep (s1 : Bool) (b : Î±) (s2 : Bool) : Îº :=
  â†‘(s1.not && (decide (b = a)) && s2).toNat

/-- `WNFA.char a` accepts a weighted language assigning the string `[a]` weight `1`, and `0` to all
other strings. -/
def char : WNFA Î± Bool Îº where
  step := charStep a
  start := charStart
  final := charFinal

@[simp]
theorem char_step : (char (Îº:=Îº) a).step = charStep (Îº:=Îº) a :=
  rfl

@[simp]
theorem char_start : (char (Îº:=Îº) a).start = charStart (Îº:=Îº) :=
  rfl

@[simp]
theorem char_final : (char (Îº:=Îº) a).final = charFinal (Îº:=Îº) :=
  rfl

@[simp]
theorem charStep_zero :
    charStep (Îº:=Îº) a true = Function.const Î± (Function.const Bool (0 : Îº)) := by
  ext b s
  simp [charStep]

theorem accepts_char : (char (Îº:=Îº) a).accepts = fun x â†¦ if x = [a] then (1 : Îº) else (0 : Îº) := by
  ext x
  rw [accepts]
  cases x with
  | nil =>
    simp
  | cons b x =>
    cases x with
    | nil =>
      by_cases h : b = a
      Â· subst b
        simp [stepSet]
      Â· rw [if_neg <| by simpa]
        simp [stepSet, decide_eq_false h]
    | cons c x =>
      simp [stepSet, acceptsFrom_smul]

end char

section union

variable {Ïƒ1 Ïƒ2 : Type v} [W : Semiring Îº]

/-- `combineSum S1 S2` disjointly adds the weights of `S1` and `S2`. -/
def combineSum (S1 : Ïƒ1 â†’ Îº) (S2 : Ïƒ2 â†’ Îº) : Ïƒ1 âŠ• Ïƒ2 â†’ Îº
| .inl s1 => S1 s1
| .inr s2 => S2 s2

section unionDef

variable (M1 : WNFA Î± Ïƒ1 Îº) (M2 : WNFA Î± Ïƒ2 Îº)

/-- `M1.unionStart M2` is the initial weighting of `M1 + M2`. -/
@[simp]
def unionStart : Ïƒ1 âŠ• Ïƒ2 â†’ Îº := combineSum M1.start M2.start

/-- `M1.unionFinal M2` is the final weighting of `M1 + M2`. -/
@[simp]
def unionFinal : Ïƒ1 âŠ• Ïƒ2 â†’ Îº := combineSum M1.final M2.final

/-- `M1.unionStep M2` is step function of `M1 + M2`. -/
@[simp]
def unionStep : Ïƒ1 âŠ• Ïƒ2 â†’ Î± â†’ Ïƒ1 âŠ• Ïƒ2 â†’ Îº
| .inl s1, a => combineSum (M1.step s1 a) (fun _ â†¦ 0)
| .inr s2, a => combineSum (fun _ â†¦ 0) (M2.step s2 a)

/-- `M1.union M2`, notated as `M1 + M2` accepts the sum of weighted languages of `M1` and `M2`. -/
def union : WNFA Î± (Ïƒ1 âŠ• Ïƒ2) Îº where
  step := unionStep M1 M2
  start := unionStart M1 M2
  final := unionFinal M1 M2

end unionDef

instance : HAdd (WNFA Î± Ïƒ1 Îº) (WNFA Î± Ïƒ2 Îº) (WNFA Î± (Ïƒ1 âŠ• Ïƒ2) Îº) :=
  âŸ¨unionâŸ©

variable {M1 : WNFA Î± Ïƒ1 Îº} {M2 : WNFA Î± Ïƒ2 Îº}

theorem hadd_eq_union : M1 + M2 = M1.union M2 :=
  rfl

@[simp]
theorem unionStart_proj : (M1 + M2).start = M1.unionStart M2 :=
  rfl

@[simp]
theorem unionFinal_proj : (M1 + M2).final = M1.unionFinal M2 :=
  rfl

@[simp]
theorem unionStep_proj : (M1 + M2).step = M1.unionStep M2 :=
  rfl

variable [Fintype Ïƒ1] [Fintype Ïƒ2]

theorem stepSet_hadd {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} {a : Î±} :
    (M1 + M2).stepSet (combineSum S1 S2) a = combineSum (M1.stepSet S1 a) (M2.stepSet S2 a) := by
  ext (s1 | s2) <;> simp [stepSet, combineSum]

theorem acceptsFrom_hadd {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} :
    (M1 + M2).acceptsFrom (combineSum S1 S2) = M1.acceptsFrom S1 + M2.acceptsFrom S2 := by
  ext x
  rw [WeightedLanguage.add_apply]
  induction x generalizing S1 S2 with
  | nil => simp [combineSum]
  | cons a x ih => simp [stepSet_hadd, ih]

theorem accepts_hadd : (M1 + M2).accepts = M1.accepts + M2.accepts := by
  simp [accepts, acceptsFrom_hadd]

end union

section inter

variable {Ïƒ1 Ïƒ2 : Type v} [W : CommSemiring Îº]

/-- `combineProd S1 S2` computes the product of weights of `S1` and `S2`. -/
def combineProd (S1 : Ïƒ1 â†’ Îº) (S2 : Ïƒ2 â†’ Îº) (s : Ïƒ1 Ã— Ïƒ2) : Îº := S1 s.1 * S2 s.2

variable (M1 : WNFA Î± Ïƒ1 Îº) (M2 : WNFA Î± Ïƒ2 Îº)

/-- `M1.interStart M2` is the initial weightings for `M1.inter M2`. -/
@[simp]
def interStart : Ïƒ1 Ã— Ïƒ2 â†’ Îº := combineProd M1.start M2.start

/-- `M1.interFinal M2` is the final weightings for `M1.inter M2`. -/
@[simp]
def interFinal : Ïƒ1 Ã— Ïƒ2 â†’ Îº := combineProd M1.final M2.final

/-- `M1.interStep M2` is the step function for `M1.inter M2`. -/
@[simp]
def interStep (s : Ïƒ1 Ã— Ïƒ2) (a : Î±) : Ïƒ1 Ã— Ïƒ2 â†’ Îº :=
  combineProd (M1.step s.1 a) (M2.step s.2 a)

/-- `M1.inter M2` is the intersection of `M1` and `M2`, accepting the Hadamard product of their
weighted languages. -/
@[simps]
def inter : WNFA Î± (Ïƒ1 Ã— Ïƒ2) Îº where
  step := M1.interStep M2
  start := M1.interStart M2
  final := M1.interFinal M2

@[simp]
theorem inter_start_eq_interStart : (M1.inter M2).start = M1.interStart M2 :=
  rfl

@[simp]
theorem inter_final_eq_interFinal : (M1.inter M2).final = M1.interFinal M2 :=
  rfl

@[simp]
theorem inter_step_eq_interStep : (M1.inter M2).step = M1.interStep M2 :=
  rfl

variable [Fintype Ïƒ1] [Fintype Ïƒ2]

theorem stepSet_inter {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} {a : Î±} :
    (M1.inter M2).stepSet (combineProd S1 S2) a
    = combineProd (M1.stepSet S1 a) (M2.stepSet S2 a) := by
  ext âŸ¨s1, s2âŸ©
  suffices h :
    âˆ‘ i, âˆ‘ j, S1 i * S2 j * (M1.step i a s1 * M2.step j a s2) =
    âˆ‘ i, âˆ‘ j, S1 i * M1.step i a s1 * (S2 j * M2.step j a s2) by
    simpa [stepSet, combineProd, Fintype.sum_mul_sum, Fintype.sum_prod_type]
  ac_nf

theorem acceptsFrom_inter {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} :
    (M1.inter M2).acceptsFrom (combineProd S1 S2)
    = (M1.acceptsFrom S1).hadamard (M2.acceptsFrom S2) := by
  ext x
  rw [WeightedLanguage.hadamard]
  induction x generalizing S1 S2 with
  | nil =>
    suffices h :
      âˆ‘ i, âˆ‘ j, S1 i * S2 j * (M1.final i * M2.final j) =
      âˆ‘ i, âˆ‘ j, S1 i * M1.final i * (S2 j * M2.final j) by
      simpa [combineProd, Fintype.sum_mul_sum, Fintype.sum_prod_type]
    ac_nf
  | cons a x ih =>
    simp [stepSet_inter, ih]

theorem accepts_inter : (M1.inter M2).accepts = M1.accepts.hadamard M2.accepts := by
  simp [accepts, acceptsFrom_inter]

end inter

section concat

variable {Ïƒ1 Ïƒ2 : Type v}

@[simp]
theorem combineSum_apply_inl {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} {s : Ïƒ1} :
    combineSum S1 S2 (Sum.inl s) = S1 s :=
  rfl

@[simp]
theorem combineSum_apply_inr {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} {s : Ïƒ2} :
    combineSum S1 S2 (Sum.inr s) = S2 s :=
  rfl

variable [W : Semiring Îº]

theorem combineSum_separate {S1 : Ïƒ1 â†’ Îº} {S2 : Ïƒ2 â†’ Îº} :
    combineSum S1 S2 = combineSum S1 0 + combineSum 0 S2 := by
  ext (s1 | s2) <;> simp

variable (M1 : WNFA Î± Ïƒ1 Îº) (M2 : WNFA Î± Ïƒ2 Îº)

/-- `M1.concatStart` is the initial weightings of `M1 * M2`. -/
@[simp]
def concatStart : Ïƒ1 âŠ• Ïƒ2 â†’ Îº :=
  combineSum M1.start 0

variable [Fintype Ïƒ2]

/-- `M1.concatFinal M2` is the final weightings of `M1 * M2`. -/
@[simp]
def concatFinal : Ïƒ1 âŠ• Ïƒ2 â†’ Îº :=
  combineSum ((Â· * M2.accepts []) âˆ˜ M1.final) M2.final

/-- `M1.concatStep M2` is the step function of `M1 * M2`.
We concatenate `M1` and `M2` by adding transitions from final states in `M1` to states subsequent
to initial states in `M2`. -/
@[simp]
def concatStep : Ïƒ1 âŠ• Ïƒ2 â†’ Î± â†’ Ïƒ1 âŠ• Ïƒ2 â†’ Îº
| .inl s1, a =>
  combineSum (M1.step s1 a) (M1.final s1 â€¢ âˆ‘ s2 : Ïƒ2, M2.start s2 â€¢ M2.step s2 a)
| .inr s2, a =>
  combineSum 0 (M2.step s2 a)

/-- `M1.concat M2`, notated as `M1 * M2`, accepts the Cauchy product of the weighted languages of
`M1` and `M2`. -/
def concat : WNFA Î± (Ïƒ1 âŠ• Ïƒ2) Îº where
  step := M1.concatStep M2
  start := M1.concatStart
  final := M1.concatFinal M2

instance : HMul (WNFA Î± Ïƒ1 Îº) (WNFA Î± Ïƒ2 Îº) (WNFA Î± (Ïƒ1 âŠ• Ïƒ2) Îº) where
  hMul M1 M2 := M1.concat M2

theorem hmul_eq_concat : M1 * M2 = M1.concat M2 := by
  rfl

@[simp]
theorem hmul_concat_step : (M1 * M2).step = M1.concatStep M2 := by
  rfl

@[simp]
theorem hmul_concat_start : (M1 * M2).start = M1.concatStart := by
  rfl

@[simp]
theorem hmul_concat_final : (M1 * M2).final = M1.concatFinal M2 := by
  rfl

variable [Fintype Ïƒ1]

theorem stepSet_hmul_inr {S2 : Ïƒ2 â†’ Îº} {a : Î±} :
    (M1 * M2).stepSet (combineSum 0 S2) a = combineSum 0 (M2.stepSet S2 a) := by
  ext (s1 | s2) <;> simp [stepSet, combineSum]

theorem acceptsFrom_hmul_inr {S2 : Ïƒ2 â†’ Îº} :
    (M1 * M2).acceptsFrom (combineSum 0 S2) = M2.acceptsFrom S2 := by
  ext y
  induction y generalizing S2 with
  | nil => simp [combineSum]
  | cons a y ih => simp [stepSet_hmul_inr, ih]

theorem acceptsFrom_hmul {S1 : Ïƒ1 â†’ Îº} :
    (M1 * M2).acceptsFrom (combineSum S1 0) = M1.acceptsFrom S1 * M2.accepts := by
  ext z
  induction z generalizing S1 with
  | nil =>
    simp [Finset.sum_mul, W.mul_assoc]
  | cons a z ih =>
    suffices h :
      âˆ‘ s1 : Ïƒ1,
        S1 s1 *
        (M1 * M2).acceptsFrom
          (combineSum (M1.step s1 a)
            (M1.final s1 â€¢ âˆ‘ s2 : Ïƒ2, M2.start s2 â€¢ M2.step s2 a)) z =
      (âˆ‘ s1 : Ïƒ1, S1 s1 * M1.final s1) * M2.accepts (a :: z) +
      (((âˆ‘ s1 : Ïƒ1, S1 s1 â€¢ M1.acceptsFrom (M1.step s1 a)) : WeightedLanguage Î± Îº)
       * M2.accepts) z by
      simpa [stepSet, acceptsFrom_sum, acceptsFrom_smul,
        Function.comp_def (fun x : Îº â†¦ (0 : Îº)),
        show (â†‘(Fintype.card Ïƒ2) * fun x â†¦ 0) = (0 : Ïƒ1 âŠ• Ïƒ2 â†’ Îº) by (ext (s1 | s2) <;> simp)]
    conv_lhs => {
      arg 2
      ext s
      rw [combineSum_separate, acceptsFrom_add, WeightedLanguage.add_apply, ih,
        acceptsFrom_hmul_inr]
    }
    open scoped Classical in
    simp [W.left_distrib, Finset.sum_add_distrib,
      acceptsFrom_smul, acceptsFrom_sum, accepts, stepSet, Finset.sum_mul,
      Finset.mul_sum, WeightedLanguage.mul_as_sum_over_prod,
      W.add_comm (âˆ‘ s : Ïƒ1, âˆ‘ y âˆˆ z.splits.toFinset, _), W.mul_assoc,
      Finset.sum_comm (f:=fun x y â†¦ S1 y * _)]

theorem accepts_hmul : (M1 * M2).accepts = M1.accepts * M2.accepts := by
  simp [accepts, acceptsFrom_hmul]

end concat

section reverse

variable {Ïƒ : Type v} (M : WNFA Î± Ïƒ Îº)

/-- `M.reverseStep` reverses transitions in `M`. -/
@[simp]
def reverseStep (s : Ïƒ) (a : Î±) (s' : Ïƒ) : Îº := M.step s' a s

/-- `M.reverse` acceptes the reversed weighted language of `M`. -/
def reverse : WNFA Î± Ïƒ Îº where
  step := M.reverseStep
  start := M.final
  final := M.start

@[simp]
theorem reverse_step_eq_reverseStep : M.reverse.step = M.reverseStep :=
  rfl

@[simp]
theorem reverse_start_eq_reverseStart : M.reverse.start = M.final :=
  rfl

@[simp]
theorem reverse_final_eq_reverseFinal : M.reverse.final = M.start :=
  rfl

variable [W : CommSemiring Îº] [Fintype Ïƒ]

theorem reverse_evalFrom_eq_evalFrom_reverse {S1 S2 : Ïƒ â†’ Îº} {x : List Î±} :
    âˆ‘ s : Ïƒ, M.reverse.evalFrom S2 x s * S1 s = âˆ‘ s : Ïƒ, M.evalFrom S1 x.reverse s * S2 s := by
  induction x generalizing S1 S2 with
  | nil => simp [W.mul_comm (S1 _) (S2 _)]
  | cons a x ih =>
    suffices h :
      âˆ‘ i, âˆ‘ j, M.evalFrom S1 x.reverse i * (S2 j * M.step i a j) =
      âˆ‘ j, âˆ‘ i, M.evalFrom S1 x.reverse i * M.step i a j * S2 j by
      simpa [stepSet, ih, Finset.mul_sum, Finset.sum_mul]
    rw [Finset.sum_comm]
    ac_nf

theorem accepts_reverse : M.reverse.accepts = M.accepts.reverse := by
  ext x
  simp [accepts, acceptsFrom, reverse_evalFrom_eq_evalFrom_reverse, WeightedLanguage.reverse]

end reverse

end WNFA

namespace WDFA

variable {Î± : Type u} {Îº : Type k} {Ïƒ : Type v} [W : Semiring Îº] [DecidableEq Ïƒ]

/-- `funOfPair sw` constructs a function mapping `sw.1` to `sw.2` and `0` to all other states. -/
def funOfPair (sw : Ïƒ Ã— Îº) (s : Ïƒ) : Îº :=
  if s = sw.1 then sw.2 else 0

/-- `M.toWNFA` constructs a WNFA from WDFA `M`. -/
@[simps]
def toWNFA (M : WDFA Î± Ïƒ Îº) : WNFA Î± Ïƒ Îº where
  step s a := funOfPair (M.step s a)
  start := funOfPair M.start
  final := M.final

variable [Fintype Ïƒ]

theorem stepSet_toWNFA (M : WDFA Î± Ïƒ Îº) (sw : Ïƒ Ã— Îº) (a : Î±) :
    M.toWNFA.stepSet (funOfPair sw) a = sw.2 â€¢ funOfPair (M.step sw.1 a) := by
  obtain âŸ¨s, wâŸ© := sw
  ext s'
  simp only [WNFA.stepSet, toWNFA, Finset.sum_apply, Pi.smul_apply]
  rw [Finset.sum_eq_add_sum_diff_singleton (Finset.mem_univ s)]
  have hzero : âˆ‘ q âˆˆ Finset.univ \ {s}, funOfPair (s, w) q â€¢ funOfPair (M.step q a) s' = 0 := by
  { apply Finset.sum_eq_zero
    intro q hdiff
    obtain hqs := Finset.notMem_singleton.mp <| (Finset.mem_sdiff.mp hdiff).2
    simp [funOfPair, if_neg hqs] }
  rw [hzero]
  simp [funOfPair]

theorem acceptsFrom_toWNFA (M : WDFA Î± Ïƒ Îº) (sw : Ïƒ Ã— Îº) :
  M.toWNFA.acceptsFrom (funOfPair sw) = M.acceptsFrom sw := by
  ext x
  induction x generalizing sw
  case nil => simp [funOfPair]
  case cons a x ih =>
    obtain âŸ¨s, wâŸ© := sw
    rcases hstep : M.step s a with âŸ¨s', w'âŸ©
    simp [acceptsFrom_cons, WNFA.acceptsFrom_cons, stepSet_toWNFA, WNFA.acceptsFrom_smul, hstep,
      acceptsFrom_prod, ih]

theorem accepts_toWNFA (M : WDFA Î± Ïƒ Îº) : M.toWNFA.accepts = M.accepts := by
  simp only [WDFA.accepts, WNFA.accepts, â†acceptsFrom_toWNFA]
  rfl

end WDFA
