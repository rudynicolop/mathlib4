/-
Copyright (c) 2025 Rudy Peterson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rudy Peterson
-/
module

public import Mathlib.Algebra.Ring.Defs
public import Mathlib.Computability.WeightedPath
public import Mathlib.Computability.WeightedLanguage
public import Mathlib.Computability.DFA
public import Mathlib.Algebra.Ring.PUnit
public import Mathlib.Algebra.Ring.WithZero
public import Mathlib.Algebra.Order.GroupWithZero.Canonical

/-!
# Weighted Deterministic Finite Automata

A Weighted Deterministic Finite Automata (WDFA) is a state machine that describes a weighted
language by assinging an input string a weight. The weight of the string is determined by
the path it takes through the state machine.

Every transition in a WDFA produces a weight, which is an element of a semiring.
The weight of a path, a sequence of transitions, is the in-order multiplication of all of its
constituent transitions.

Note that this definition allows for automata with infinite states,
a `Fintype` instance must be supplied for true DFAs.

Note that since WDFA only use multiplication, we only require a monoid for multiplication, not a
full semiring.

TODO: explain stuff.
-/

@[expose] public section

universe k u v

/-- A Weighted DFA (`ð“`) over a semiring (`ð“¦ = (Îº, âŠ•, âŠ—, 0, 1)`)
is a 5-tuple (`(Î±, Ïƒ, step, start, final)`) where
* (`Î±`) is a (finite) alphabet.
* (`Ïƒ`) is a (finite) set of states.
* (`step : Ïƒ â†’ Î± â†’ Ïƒ Ã— Îº`) is a (finite) set of transitions.
* (`start : Ïƒ Ã— Îº`) the start state and its weight.
* (`final : Ïƒ â†’ Îº`) is a weighting function assigning states their final values.
-/
structure WDFA (Î± : Type u) (Ïƒ : Type v) (Îº : Type k) where
  /-- A transition function from state to state labelled by the alphabet producing a weight. -/
  step : Ïƒ â†’ Î± â†’ Ïƒ Ã— Îº
  /-- Starting state and weight. -/
  start : Ïƒ Ã— Îº
  /-- Final weights. -/
  final : Ïƒ â†’ Îº

namespace WDFA

variable {Î± : Type u}

section basic

variable {Îº : Type k} {Ïƒ : Type v} (M : WDFA Î± Ïƒ Îº) [W : MonoidWithZero Îº]

instance [Inhabited Ïƒ] [Inhabited Îº] : Inhabited (WDFA Î± Ïƒ Îº) :=
  âŸ¨WDFA.mk (fun _ _ => âŸ¨default, defaultâŸ©) âŸ¨default, defaultâŸ© (fun _ â†¦ 0)âŸ©

/-- `M.PathInWDFA Ï€` holds when `Ï€` is a valid sequence of transitions in `M`. -/
def PathInWDFA {sâ‚ sâ‚ƒ : Ïƒ} : WeightedPath Î± Îº sâ‚ sâ‚ƒ â†’ Prop :=
  WeightedPath.All (fun qâ‚ a w qâ‚‚ â†¦ M.step qâ‚ a = (qâ‚‚, w))

/-- `M.AcceptingPathInWDFA Ï€` holds when `Ï€` is a valid path in `M` from a start state to a final
state yielding weight `w`. -/
def AcceptingPathInWDFA {sâ‚ sâ‚‚ : Ïƒ} (Ï€ : WeightedPath Î± Îº sâ‚ sâ‚‚) (w : Îº) : Prop :=
  sâ‚ = M.start.1 âˆ§
  M.PathInWDFA Ï€ âˆ§
  w = M.start.2 * Ï€.innerWeight * M.final sâ‚‚

/--
`M.evalFromL s x` evaluates `M` with input `x` starting from
the state `s` left-associatively. -/
def evalFromL : Ïƒ Ã— Îº â†’ List Î± â†’ Ïƒ Ã— Îº :=
  List.foldl (fun sw a â†¦ Prod.map id (W.mul sw.2) (M.step sw.1 a))

@[simp]
lemma evalFromL_nil (sw : Ïƒ Ã— Îº) : M.evalFromL sw [] = sw := rfl

@[simp]
lemma evalFromL_cons (sw : Ïƒ Ã— Îº) (a : Î±) (x : List Î±) :
    M.evalFromL sw (a :: x) = M.evalFromL (Prod.map id (sw.2 * Â·) (M.step sw.1 a)) x := by
  simp only [evalFromL, List.foldl_cons]
  congr

@[simp]
lemma evalFromL_append (sw : Ïƒ Ã— Îº) (x y : List Î±) :
    M.evalFromL sw (x ++ y) = M.evalFromL (M.evalFromL sw x) y := by
  simp only [evalFromL, List.foldl_append]

lemma evalFromL_singleton (sw : Ïƒ Ã— Îº) (a : Î±) :
    M.evalFromL sw [a] = Prod.map id (sw.2 * Â·) (M.step sw.1 a) := rfl

lemma evalFromL_append_singleton (sw : Ïƒ Ã— Îº) (x : List Î±) (a : Î±) :
    M.evalFromL sw (x ++ [a]) =
    Prod.map id ((M.evalFromL sw x).2 * Â·) (M.step (M.evalFromL sw x).1 a) := by
  simp

lemma evalFromL_prod (s : Ïƒ) (w1 w2 : Îº) (x : List Î±) :
    M.evalFromL (s, w1 * w2) x =
    Prod.map id (w1 * Â·) (M.evalFromL (s, w2) x) := by
  induction x generalizing s w1 w2
  case nil =>
    simp
  case cons a x ih =>
    rcases hstep : (M.step s a) with âŸ¨s', w'âŸ©
    simp only [evalFromL_cons, hstep, Prod.map_apply, id_eq, â† ih]
    ac_nf

lemma evalFromL_prod_one (s : Ïƒ) (w : Îº) (x : List Î±) :
    M.evalFromL (s, w) x =
    Prod.map id (w * Â·) (M.evalFromL (s, 1) x) := by
  simp [â†evalFromL_prod, W.mul_one]

/-- `M.eval x` evaluates `M` with input `x` starting from the state `M.start`. -/
def eval : List Î± â†’ Ïƒ Ã— Îº := M.evalFromL M.start

/-- `M.evalWeight x` evaluates `M` with input `x` starting from the state `M.start` producing the
final weight. -/
def evalWeight : WeightedLanguage Î± Îº :=
  fun x â†¦
    let (s, w) := M.eval x;
    w * M.final s

@[simp]
lemma eval_nil : M.eval [] = M.start := rfl

@[simp]
lemma eval_singleton (a : Î±) : M.eval [a] = Prod.map id (M.start.2 * Â·) (M.step M.start.1 a) := by
  simp only [eval, evalFromL_singleton]

@[simp]
lemma eval_append_singleton (x : List Î±) (a : Î±) :
    M.eval (x ++ [a]) = Prod.map id ((M.eval x).2 * Â·) (M.step (M.eval x).1 a) := by
  simp only [eval, evalFromL_append_singleton]

/-- `M.acceptsFrom sw x` is the weighted lenaguage of `x` such that `(M.evalFromL sw x).1` is an
accept state. -/
def acceptsFrom (sw : Ïƒ Ã— Îº) : WeightedLanguage Î± Îº :=
  fun x â†¦
    let (sâ‚‚, w) := (M.evalFromL sw x);
    w * M.final sâ‚‚

@[simp]
lemma acceptsFrom_nil (sw : Ïƒ Ã— Îº) : M.acceptsFrom sw [] = sw.2 * M.final sw.1 :=
  rfl

@[simp]
lemma acceptsFrom_cons (sw : Ïƒ Ã— Îº) (a : Î±) (x : List Î±) :
    M.acceptsFrom sw (a :: x) = M.acceptsFrom ((M.step sw.1 a).1, (sw.2 * (M.step sw.1 a).2)) x :=
  rfl

lemma acceptsFrom_prod (s : Ïƒ) (w1 w2 : Îº) (x : List Î±) :
    M.acceptsFrom (s, w1 * w2) x =
    w1 * M.acceptsFrom (s, w2) x := by
  simp [acceptsFrom, evalFromL_prod, mul_assoc]

lemma acceptsFrom_prod_one (s : Ïƒ) (w : Îº) (x : List Î±) :
    M.acceptsFrom (s, w) x =
    w * M.acceptsFrom (s, 1) x := by
  simp [â†acceptsFrom_prod]

@[simp]
lemma acceptsFrom_zero (s : Ïƒ) (x : List Î±) : M.acceptsFrom (s, 0) x = 0 := by
  induction x generalizing s
  case nil => simp
  case cons a x ih => simp [ih]

/-- `M.accepts x` is the weighted lenaguage of `x` such that `(M.evalFromL M.start x).1` is an
accept state. -/
def accepts : WeightedLanguage Î± Îº := M.acceptsFrom M.start

theorem weight_accepts (x : List Î±) : M.accepts x = M.evalWeight x :=
  rfl

end basic

section inter

variable {Îº : Type k} {Ïƒ1 Ïƒ2 : Type v} [W : CommMonoidWithZero Îº]

@[simp]
def interStart (M1 : WDFA Î± Ïƒ1 Îº) (M2 : WDFA Î± Ïƒ2 Îº) : ((Ïƒ1 Ã— Ïƒ2) Ã— Îº) :=
  ((M1.start.1, M2.start.1), M1.start.2 * M2.start.2)

@[simp]
def interFinal (M1 : WDFA Î± Ïƒ1 Îº) (M2 : WDFA Î± Ïƒ2 Îº) (s : Ïƒ1 Ã— Ïƒ2) : Îº :=
  M1.final s.1 * M2.final s.2

@[simp]
def interStep (M1 : WDFA Î± Ïƒ1 Îº) (M2 : WDFA Î± Ïƒ2 Îº) (s : Ïƒ1 Ã— Ïƒ2) (a : Î±) : (Ïƒ1 Ã— Ïƒ2) Ã— Îº :=
  let sw1 := M1.step s.1 a;
  let sw2 := M2.step s.2 a;
  ((sw1.1, sw2.1), sw1.2 * sw2.2)

@[simps]
def inter (M1 : WDFA Î± Ïƒ1 Îº) (M2 : WDFA Î± Ïƒ2 Îº) : WDFA Î± (Ïƒ1 Ã— Ïƒ2) Îº where
  start := interStart M1 M2
  final := interFinal M1 M2
  step := interStep M1 M2

lemma acceptsFrom_inter {M1 : WDFA Î± Ïƒ1 Îº} {M2 : WDFA Î± Ïƒ2 Îº}
  {s1 : Ïƒ1} {s2 : Ïƒ2} {w1 w2 : Îº} :
    (M1.inter M2).acceptsFrom ((s1, s2), w1 * w2)
    = (M1.acceptsFrom (s1, w1)).pointwise_prod (M2.acceptsFrom (s2, w2)) := by
  ext x
  rw [WeightedLanguage.pointwise_prod_apply]
  induction x generalizing s1 s2 w1 w2
  case nil =>
    simp only [acceptsFrom_nil, inter_final, interFinal]
    ac_nf
  case cons a x ih =>
    simp only [acceptsFrom_cons, inter_step, interStep, ih]
    rcases (M1.step s1 a) with âŸ¨s1', w1'âŸ©
    rcases (M2.step s2 a) with âŸ¨s2', w2'âŸ©
    simp only [acceptsFrom_prod]
    rw [acceptsFrom_prod_one M1 s1' w2,
        acceptsFrom_prod_one M1 s1' w1',
        acceptsFrom_prod_one M2 s2' w2']
    ac_nf

theorem accepts_inter {M1 : WDFA Î± Ïƒ1 Îº} {M2 : WDFA Î± Ïƒ2 Îº} :
    (M1.inter M2).accepts = M1.accepts.pointwise_prod M2.accepts := by
  simp [accepts, acceptsFrom_inter]

end inter

section toDFA

/- ### Weighted to unweighted DFA

We cannot use `Bool` for the weight type, since the Mathlib instance for `Add Bool` uses `xor`, not
`or`. Instead we use a type isomorphic to `Bool`.

-/

variable {Ïƒ : Type v} (M : WDFA Î± Ïƒ (WithZero Unit))

@[simp]
def toDFAStart : Option Ïƒ :=
  if M.start.2 = 1 then .some M.start.1 else .none

@[simp]
def toDFAAccept : Set (Option Ïƒ) :=
  { so | âˆƒ s, M.final s = 1 âˆ§ so = .some s }

@[simp]
def toDFAStep : Option Ïƒ â†’ Î± â†’ Option Ïƒ
| .none, _ => .none
| .some s, a =>
  let âŸ¨s', wâŸ© := M.step s a;
  if w = 1 then .some s' else none

@[simps]
def toDFA : DFA Î± (Option Ïƒ) where
  step := M.toDFAStep
  start := M.toDFAStart
  accept := M.toDFAAccept

lemma toDFA_acceptsFrom_none {x : List Î±} : x âˆ‰ M.toDFA.acceptsFrom .none := by
  induction x
  case nil => simp
  case cons a x ih => simpa

lemma wzu_zero_or_one (w : WithZero Unit) : w = 0 âˆ¨ w = 1 :=
  match w with
  | .none => by tauto
  | .some .unit => by tauto

lemma toDFA_acceptsFrom {s : Ïƒ} {x : List Î±} :
    x âˆˆ M.toDFA.acceptsFrom (.some s) â†” M.acceptsFrom (s, 1) x = 1 := by
  induction x generalizing s
  case nil => simp
  case cons a x ih =>
    rcases hstep : M.step s a with âŸ¨s', wâŸ©
    simp [hstep]
    rcases (wzu_zero_or_one w) with rfl | rfl
    Â· simp [toDFA_acceptsFrom_none]
    Â· simp [ih]

theorem toDFA_accepts {x : List Î±} :
    x âˆˆ M.toDFA.accepts â†” M.accepts x = 1 := by
  simp [accepts, DFA.accepts]
  rcases M.start with âŸ¨s, wâŸ©
  rcases (wzu_zero_or_one w) with rfl | rfl
  Â· simp [toDFA_acceptsFrom_none]
  Â· simp [toDFA_acceptsFrom]

end toDFA

end WDFA

namespace DFA

variable {Î± : Type u} {Îº : Type k} {Ïƒ : Type v} (M : DFA Î± Ïƒ) [W : CommMonoidWithZero Îº]

/- We need to assume that the set of final states is finite. -/
variable [Fintype M.accept] [DecidableEq Ïƒ]
attribute [local instance] Set.decidableMemOfFintype

@[simp]
def toWDFAStart : Ïƒ Ã— Îº := (M.start, 1)

@[simp]
def toWDFAFinal (s : Ïƒ) : Îº :=
  if s âˆˆ M.accept then 1 else 0

@[simp]
def toWDFAStep (s : Ïƒ) (a : Î±) : Ïƒ Ã— Îº := (M.step s a, 1)

@[simps]
def toWDFA : WDFA Î± Ïƒ Îº where
  step := M.toWDFAStep
  start := M.toWDFAStart
  final := M.toWDFAFinal

lemma toWDFA_acceptsFrom {x : List Î±} {s : Ïƒ} {w : Îº} (hwâ‚€ : w â‰  0) :
    M.toWDFA.acceptsFrom (s, w) x = w â†” x âˆˆ M.acceptsFrom s := by
  induction x generalizing s w
  case nil => simp; tauto
  case cons a x ih => simp [ih hwâ‚€]

lemma toWDFA_accetps {x : List Î±} : M.toWDFA.accepts x = 1 â†” x âˆˆ M.accepts := by
  simp [WDFA.accepts, accepts, toWDFA_acceptsFrom]

end DFA
